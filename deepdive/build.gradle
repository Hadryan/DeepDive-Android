buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.27.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'idea' //Add to see source code instead of decompiled jar code
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

def versionMajor = 0
def versionMinor = 9
def versionPatch = 8
def versionBuild = 1

// Load keystore
def keystorePropertiesPath = System.getProperty("user.home")+"/.gradle/keystore.properties"
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesPath))
} catch (FileNotFoundException exception) {
    keystoreProperties.setProperty('DD_keyAlias','dummy')
    keystoreProperties.setProperty('DD_storeFile','dummy')
    keystoreProperties.setProperty('DD_storePassword','dummy')
    keystoreProperties.setProperty('DD_keyPassword','dummy')
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['DD_keyAlias']
            storeFile file(keystoreProperties['DD_storeFile'])
            storePassword keystoreProperties['DD_storePassword']
            keyPassword keystoreProperties['DD_keyPassword']
        }
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.nuvolect.deepdive"
        minSdkVersion 24
        targetSdkVersion 28
        multiDexEnabled true

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // UnsatisfiedLinkError Problem on Some Android Devices
        // https://medium.com/mobiwise-blog/unsatisfiedlinkerror-problem-on-some-android-devices-b77f2f83837d
        ndk {
            abiFilters "armeabi-v7a", "x86", "armeabi", "mips"
        }
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            manifestPlaceholders = [authorities:"com.nuvolect.deepdive.fileprovider"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [authorities:"com.nuvolect.deepdive.fileprovider-release"]
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Required for local unit tests (JUnit 4 framework)
    implementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.12'
    // Required for instrumented tests
    testImplementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.test:rules:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'com.squareup.okio:okio:1.6.0'
    implementation 'com.squareup.okhttp:okhttp:2.6.0'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.6.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.4'
    implementation 'info.guardianproject.iocipher:IOCipher:0.4'
    implementation 'com.google.code.gson:gson:2.8.2'
    // APACHE COMMONS LIBRARIES
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-lang3:3.7'
    // MULTI-DEX SUPPORT FOR PRE LOLLIPOP
    implementation 'androidx.multidex:multidex:2.0.1'
    // DEX2JAR
    implementation 'com.google.guava:guava:20.0'
    //Upgrade breaks build
    // Program type already present: javax.annotation.Nonnull$Checker
    //    api 'com.google.guava:guava:27.0.1-android'
    implementation 'asm:asm-all:3.3.1'
    implementation 'org.antlr:antlr:3.5.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'uk.com.robust-it:cloning:1.9.2'
    implementation 'com.intellij:annotations:12.0@jar'
    api 'com.jaredrummler:apk-parser:1.0.2'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.58.0.0'
    implementation 'com.madgag.spongycastle:bcpg-jdk15on:1.58.0.0'

    implementation ('org.smali:dexlib2:2.2.4'){
        exclude group: 'com.google.code.findbugs'
        exclude module: 'annotations'
    }
}
//Add to see source code instead of decompiled jar code
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
